plugins {
    id 'fabric-loom' version "${loom_version}"
    id 'maven-publish'
}

// Apply common dependencies
apply from: '../dependencies.gradle'

// Function to replace placeholders in export format
def resolveExportFormat(String format) {
    return format
        .replace('{mod-name}', rootProject.mod_id)
        .replace('{mod-version}', rootProject.version)  // Use root project version for consistency
        .replace('{mc-version}', project.minecraft_version)
        .replace('{mc-version-dashed}', project.minecraft_version.replace('.', '-'))
}

// Override export format for this version (optional)
def versionExportFormat = project.hasProperty('export_format_override') ?
    project.export_format_override : rootProject.export_format

// Set version to empty to prevent Gradle from appending it to jar names
version = ''

base {
    archivesName = resolveExportFormat(versionExportFormat)
}

dependencies {
    // Version-specific dependencies (Fabric core dependencies)
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

    // Override or exclude common dependencies here if needed
    // Example: override a common dependency version
    // modImplementation('com.example:common-mod') {
    //     version {
    //         strictly '2.0.0'
    //     }
    // }

    // Example: disable/exclude a common dependency
    // configurations.modImplementation.exclude group: 'com.example', module: 'unwanted-mod'
}

// Include sources from core
sourceSets.main.java.srcDirs += file('../core/src/main/java')
sourceSets.main.resources.srcDirs += file('../core/src/main/resources')

processResources {
    inputs.property "version", rootProject.version

    filesMatching("fabric.mod.json") {
        expand "version": rootProject.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = Integer.parseInt(project.java_version)
}

java {
    withSourcesJar()
    sourceCompatibility = JavaVersion.toVersion(project.java_version)
    targetCompatibility = JavaVersion.toVersion(project.java_version)
}

jar {
    doFirst {
        println("")
        println "=========================================================================="
        println "Building mod using fabric-multi-version-mod-template by shyskyfox aka Lina"
        println "https://github.com/shyskyfox/fabric-multi-version-mod-template"
        println "=========================================================================="
    }
    from("../../LICENSE") {
        rename { "${it}_${project.base.archivesName}"}
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        // Publishing repositories
    }
}
